// SL 2019-10
//
// @sylefeb 2020
// https://github.com/sylefeb/Silice
// MIT license, see LICENSE_MIT in Silice repo root

unit vga(
  output uint1  vga_hs(0),
  output uint1  vga_vs(0),
  output uint1  active,
  output uint1  vblank,
  output uint12 vga_x,
  output uint11 vga_y
) {

//_ default to 640x480@60 (25MHz pixel clock)
$$  H_FRT_PORCH = 16
$$  H_SYNCH     = 96
$$  H_BCK_PORCH = 48
$$  H_RES       = 640
//
$$  V_FRT_PORCH = 10
$$  V_SYNCH     = 2
$$  V_BCK_PORCH = 33
$$  V_RES       = 480

//
$$if not VGA_VA_END then
$$VGA_VA_END = V_RES
$$end
//
$$HS_START = H_FRT_PORCH
$$HS_END   = H_FRT_PORCH + H_SYNCH
$$HA_START = H_FRT_PORCH + H_SYNCH + H_BCK_PORCH
$$H_END    = H_FRT_PORCH + H_SYNCH + H_BCK_PORCH + H_RES
//
$$VS_START = V_FRT_PORCH
$$VS_END   = V_FRT_PORCH + V_SYNCH
$$VA_START = V_FRT_PORCH + V_SYNCH + V_BCK_PORCH
$$V_END    = V_FRT_PORCH + V_SYNCH + V_BCK_PORCH + V_RES

  int11  xcount(0);
  int10  ycount(0);

  uint1  active_h(0);
  uint1  active_v(0);

  always {

    active_h  = xcount == 0                  ? 1
              : xcount == $H_END-HA_START$   ? 0
              : active_h;
    active_v  = ycount == 0                  ? 1
              : ycount == $VGA_VA_END$       ? 0
              : active_v;

    // __display("%d %d h:%b v:%b",xcount,ycount,active_h,active_v);

    active = active_h && active_v;

    vga_x  = active_h ? xcount : 0;
    vga_y  = active_v ? ycount : 0;

    vga_hs = xcount == $HS_START-HA_START$ ? 0
           : xcount == $HS_END-HA_START-1$ ? 1
           : vga_hs;
    vga_vs = ycount == $VS_START-VA_START$ ? 0
           : ycount == $VS_END-VA_START-1$ ? 1
           : vga_vs;

    vblank = ycount[9,1]; // sign bit

    if (xcount == $H_END-HA_START$) {
      xcount = __signed($-HA_START+1$);
      if (ycount == $V_END-VA_START$) {
        ycount = __signed($-VA_START+1$);
      } else {
        ycount = ycount + 1;
	    }
    } else {
      xcount = xcount + 1;
	  }
$$if VERILATOR then
    __verilog("$c32(\"set_vga_resolution(640,480);\");");
$$end
  }

}

// -------------------------
